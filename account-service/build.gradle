plugins {
    id("account.service")
}

dependencies {
    implementation(project(":eventstore"))

    implementation(libs.sparkJava) {
        exclude(group: "org.eclipse.jetty.websocket", module: "websocket-server")
        exclude(group: "org.eclipse.jetty.websocket", module: "websocket-servlet")
        exclude(group: "org.eclipse.jetty", module: "jetty-webapp")
    }

    implementation(libs.msgpack)
    implementation(libs.logback)
    implementation(libs.hikari)

    agent(libs.opentelemetryAgent)

    testFixturesApi(libs.junit)
    testFixturesApi(libs.assertj)

    testImplementation(libs.archunit)
    testImplementation(libs.restAssured)
    testImplementation(libs.dbTestH2)

    blackBoxTestImplementation(libs.junit)
    blackBoxTestImplementation(libs.assertj)
    blackBoxTestImplementation(libs.testcontainers)
    blackBoxTestImplementation(libs.apacheHttpClient)
    blackBoxTestImplementation(libs.apacheHttpAsyncClient)
    blackBoxTestImplementation(libs.logback)
}

tasks.named("blackBoxTest").configure {
    inputs.files("../infrastructure/").withPathSensitivity(PathSensitivity.RELATIVE)
}

application {
    mainClass = "lt.rieske.accounts.App"
}

tasks.register("stressTest", JavaExec) {
    it.classpath = sourceSets.blackBoxTest.runtimeClasspath
    it.mainClass.set("lt.rieske.accounts.StressTests")
}

tasks.register("asyncStressTest", JavaExec) {
    it.classpath = sourceSets.blackBoxTest.runtimeClasspath
    it.mainClass.set("lt.rieske.accounts.AsyncStressTests")
}

tasks.register("composeUp", Exec) {
    it.dependsOn("build")
    def composeFile = "docker-compose.yml"
    it.inputs.files(file("${rootProject.projectDir}/$composeFile")).withPathSensitivity(PathSensitivity.RELATIVE)
    it.workingDir(rootProject.projectDir)
    it.executable("docker")
    it.args("compose", "-f", "$composeFile", "up", "--build", "-d")
    it.doLast {
        println("\n\n")
        println("Zipkin:\t\t\t\thttp://localhost:9411")
        println("Grafana:\t\t\thttp://localhost:3000")
        println("Prometheus:\t\t\thttp://localhost:9090")
        println("account-service (via Envoy):\thttp://localhost:8080")
    }
}

tasks.register("composeDown", Exec) {
    it.workingDir(rootProject.projectDir)
    it.executable("docker")
    it.args("compose", "down")
}
