plugins {
    id("jacoco-report-aggregation")
    id("com.github.kt3k.coveralls")
}

// Task to aggregate code coverage reports from test and integrationTest reports
def codeCoverageReport = tasks.register("codeCoverageReport", JacocoReport) {
    it.sourceSets(sourceSets.main)
    it.additionalSourceDirs(tasks.named("testCodeCoverageReport").map { it.allSourceDirs }.get())
    it.additionalClassDirs(tasks.named("testCodeCoverageReport").map { it.allClassDirs }.get())
    it.executionData(tasks.named("testCodeCoverageReport").map { it.executionData })
    it.executionData(tasks.named("integrationTestCodeCoverageReport").map { it.executionData })

    it.reports {
        xml.required = true
        html.required = true
    }
}

tasks.named("check") {
    it.dependsOn(codeCoverageReport)
}

coveralls {
    jacocoReportPath = "build/reports/jacoco/codeCoverageReport/codeCoverageReport.xml"
}

tasks.named("coveralls") {
    doFirst {
        coveralls {
            sourceDirs += tasks.named("testCodeCoverageReport").map { it.allSourceDirs }.get().files
            sourceDirs += sourceSets.main.java.srcDirs
        }
    }
}
