plugins {
    id("internal.jacoco")
    id("com.github.kt3k.coveralls")
}

configurations {
    // A resolvable configuration to collect source code
    sourcesPath {
        visible = false
        canBeResolved = true
        canBeConsumed = false
        extendsFrom(configurations.implementation)
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
            attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, "source-folders"))
        }
    }

    // A resolvable configuration to collect JaCoCo coverage data
    coverageDataPath {
        visible = false
        canBeResolved = true
        canBeConsumed = false
        extendsFrom(configurations.implementation)
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
            attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, "jacoco-coverage-data"))
        }
    }
}

// Task to gather code coverage from multiple subprojects
def codeCoverageReport = tasks.register("codeCoverageReport", JacocoReport) {
    it.additionalClassDirs(configurations.runtimeClasspath.asFileTree.filter { it.path.contains("/build/libs/") })
    it.additionalSourceDirs(configurations.sourcesPath)
    it.executionData(configurations.coverageDataPath.incoming.artifactView { it.lenient(true) }.files.filter { it.exists() })

    it.reports {
        xml.enabled true
        html.enabled true
    }
}

tasks.named("check") {
    it.dependsOn(codeCoverageReport)
}

coveralls {
    jacocoReportPath = "build/reports/jacoco/codeCoverageReport/codeCoverageReport.xml"
}

tasks.named("coveralls") {
    doFirst {
        println(configurations.sourcesPath.flatten())
        coveralls {
            sourceDirs = configurations.sourcesPath.flatten()
        }
    }
}