plugins {
    id("account.java")
    id("account.code-coverage-aggregation")
    id("application")
    id("com.palantir.docker")
}

testing.suites {
    e2eTest(JvmTestSuite) {
        targets {
            all {
                testTask.configure {
                    dependsOn("docker")
                    inputs.files(file("Dockerfile"), tasks.named("distTar")).withPathSensitivity(PathSensitivity.RELATIVE)
                }
            }
        }
    }
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.named("build") {
    it.dependsOn(tasks.named("docker"))
}

docker {
    name = "${rootProject.name}:snapshot"
    files(tasks.named("distTar"))
}

tasks.named("check") { it.dependsOn(testing.suites.e2eTest) }
