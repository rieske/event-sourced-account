plugins {
    id("internal.java")
    id("internal.jacoco")
    id("java-test-fixtures")
}

// Do not generate reports for individual projects
tasks.named("jacocoTestReport") {
    enabled = false
}

configurations {
    // Share sources folder with other projects for aggregated JaCoCo reports
    transitiveSourcesElements {
        visible = false
        canBeResolved = false
        canBeConsumed = true
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
            attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, "source-folders"))
        }
        afterEvaluate {
            sourceSets.forEach {
                it.java.srcDirs.forEach {
                    outgoing.artifact(it)
                }
            }
        }
    }

    // Share the coverage data to be aggregated for the whole product
    coverageDataElements {
        visible = false
        canBeResolved = false
        canBeConsumed = true
        attributes {
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
            attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, "jacoco-coverage-data"))
        }
        afterEvaluate {
            tasks.withType(Test).forEach {
                outgoing.artifact(it.extensions.getByType(JacocoTaskExtension).destinationFile)
            }
        }
    }
}
