plugins {
    id("jacoco")
    id("java")
    id("java-test-fixtures")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

testing.suites {
    integrationTest(JvmTestSuite) {
        targets {
            all {
                testTask.configure {
                    shouldRunAfter(test)
                }
            }
        }
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    maxParallelForks = 8
    systemProperties["junit.jupiter.execution.parallel.enabled"] = true
    systemProperties["junit.jupiter.execution.parallel.mode.default"] = "concurrent"
}

tasks.named("check") { it.dependsOn(testing.suites.integrationTest) }
