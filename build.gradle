plugins {
    id "java"
    id 'org.beryx.runtime' version '1.8.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

task integrationTest(type: Test)
task e2eTest(type: Test)

tasks {
    wrapper {
        gradleVersion = "6.0.1"
    }

    test {
        useJUnitPlatform {
            excludeTags("integration")
            excludeTags("e2e")
        }
        maxParallelForks = 8
    }

    integrationTest {
        dependsOn(test)
        useJUnitPlatform {
            includeTags("integration")
        }
        maxParallelForks = 8
    }

    e2eTest {
        dependsOn(test)
        dependsOn(integrationTest)
        dependsOn("runtime")
        useJUnitPlatform {
            includeTags("e2e")
        }
        maxParallelForks = 8
    }

    check.dependsOn(e2eTest)
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
    }
}

dependencies {
    annotationProcessor("org.projectlombok:lombok:1.18.10")
    compileOnly("org.projectlombok:lombok:1.18.10")

    implementation("com.sparkjava:spark-core:2.9.1") {
        exclude(group: "org.eclipse.jetty.websocket", module: "websocket-server")
        exclude(group: "org.eclipse.jetty.websocket", module: "websocket-servlet")
        exclude(group: "org.eclipse.jetty", module: "jetty-webapp")
    }

    implementation("org.msgpack:msgpack-core:0.8.20")

    implementation("org.flywaydb:flyway-core:6.1.3")

    implementation("ch.qos.logback:logback-classic:1.2.3")
    implementation("io.micrometer:micrometer-registry-prometheus:1.3.2")
    implementation("io.zipkin.brave:brave-instrumentation-sparkjava:5.9.1")
    implementation("io.zipkin.brave:brave-instrumentation-p6spy:5.9.1")
    implementation("io.zipkin.reporter2:zipkin-sender-urlconnection:2.11.1")

    implementation("com.h2database:h2:1.4.200")
    implementation("mysql:mysql-connector-java:8.0.18") {
        exclude(group: "com.google.protobuf", module: "protobuf-java")
    }
    implementation("com.zaxxer:HikariCP:3.4.1")

    testAnnotationProcessor("org.projectlombok:lombok:1.18.10")
    testCompileOnly("org.projectlombok:lombok:1.18.10")
    testImplementation("org.assertj:assertj-core:3.14.0")
    testImplementation("io.rest-assured:rest-assured:4.1.2")
    testImplementation("org.reflections:reflections:0.9.11")

    testImplementation("org.testcontainers:mysql:1.12.4") {
        exclude(group: "org.slf4j", module: "slf4j-api")
    }

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.2")

    testImplementation("com.tngtech.archunit:archunit-junit5-api:0.12.0")
    testRuntimeOnly("com.tngtech.archunit:archunit-junit5-engine:0.12.0") {
        exclude(group: "org.junit.platform", module: "junit-platform-engine")
    }
}

application {
    mainClassName = "lt.rieske.accounts.App"
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = [
            'java.sql',
            'java.logging',
            'java.naming',
            'java.management']
}