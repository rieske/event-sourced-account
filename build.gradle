plugins {
    id "java"
    id 'org.beryx.runtime' version '1.8.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14
}

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.test.output
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output
    }
    e2eTest
}

def integrationTest = tasks.register('integrationTest', Test) {
    shouldRunAfter(test)

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}
def e2eTest = tasks.register('e2eTest', Test) {
    dependsOn("runtime")
    shouldRunAfter(integrationTest)

    inputs.files(jar.archiveFile, 'Dockerfile', fileTree("infrastructure"))

    testClassesDirs = sourceSets.e2eTest.output.classesDirs
    classpath = sourceSets.e2eTest.runtimeClasspath
}
tasks.named('check') { dependsOn(test, integrationTest, e2eTest) }

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}
tasks.withType(Test) {
    useJUnitPlatform()
    maxParallelForks = 8
    jvmArgs += "--enable-preview"
}
tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }
}

dependencies {
    implementation("com.sparkjava:spark-core:2.9.1") {
        exclude(group: "org.eclipse.jetty.websocket", module: "websocket-server")
        exclude(group: "org.eclipse.jetty.websocket", module: "websocket-servlet")
        exclude(group: "org.eclipse.jetty", module: "jetty-webapp")
    }

    implementation("org.msgpack:msgpack-core:0.8.20")

    implementation("org.flywaydb:flyway-core:6.3.2")

    implementation("ch.qos.logback:logback-classic:1.2.3")
    implementation("io.micrometer:micrometer-registry-prometheus:1.4.0")
    implementation("io.zipkin.brave:brave-instrumentation-sparkjava:5.10.2")
    implementation("io.zipkin.brave:brave-instrumentation-p6spy:5.10.2")
    implementation("io.zipkin.reporter2:zipkin-sender-urlconnection:2.12.1")

    implementation("com.h2database:h2:1.4.200")
    implementation("mysql:mysql-connector-java:8.0.19") {
        exclude(group: "com.google.protobuf", module: "protobuf-java")
    }
    implementation("com.zaxxer:HikariCP:3.4.2")

    testImplementation("org.assertj:assertj-core:3.15.0")
    testImplementation("io.rest-assured:rest-assured:4.3.0")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.6.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.1")

    testImplementation("com.tngtech.archunit:archunit-junit5-api:0.13.1")
    testRuntimeOnly("com.tngtech.archunit:archunit-junit5-engine:0.13.1") {
        exclude(group: "org.junit.platform", module: "junit-platform-engine")
    }

    integrationTestImplementation("org.testcontainers:mysql:1.13.0") {
        exclude(group: "org.slf4j", module: "slf4j-api")
    }

    e2eTestImplementation("org.junit.jupiter:junit-jupiter-api:5.6.1")
    e2eTestRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.1")

    e2eTestImplementation("org.assertj:assertj-core:3.15.0")
    e2eTestImplementation("org.testcontainers:testcontainers:1.13.0") {
        exclude(group: "org.slf4j", module: "slf4j-api")
    }
    e2eTestImplementation("org.slf4j:slf4j-api:1.7.30") {
        because("there is an internal slf4j version conflict in testcontainers library")
    }
    e2eTestImplementation("org.apache.httpcomponents:httpclient:4.5.3")
}

application {
    mainClassName = "lt.rieske.accounts.App"
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = [
            'java.sql',
            'java.logging',
            'java.naming',
            'java.management']
}