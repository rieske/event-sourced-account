plugins {
    id "java"
    id "application"
}

java {
    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14
}

repositories {
    mavenCentral()
}

task integrationTest(type: Test)
task e2eTest(type: Test)

tasks {
    test {
        useJUnitPlatform {
            excludeTags("integration")
            excludeTags("e2e")
        }
        maxParallelForks = 8
    }

    integrationTest {
        dependsOn(test)
        useJUnitPlatform {
            includeTags("integration")
        }
        maxParallelForks = 8
    }

    e2eTest {
        dependsOn(test)
        dependsOn(integrationTest)
        dependsOn(assemble)
        useJUnitPlatform {
            includeTags("e2e")
        }
        maxParallelForks = 8
    }

    check.dependsOn(e2eTest)
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}
tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}
tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}

configurations {
    all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }
}

dependencies {
    implementation("com.sparkjava:spark-core:2.9.1") {
        exclude(group: "org.eclipse.jetty.websocket", module: "websocket-server")
        exclude(group: "org.eclipse.jetty.websocket", module: "websocket-servlet")
        exclude(group: "org.eclipse.jetty", module: "jetty-webapp")
    }

    implementation("org.msgpack:msgpack-core:0.8.20")

    implementation("org.flywaydb:flyway-core:6.3.1")

    implementation("ch.qos.logback:logback-classic:1.2.3")
    implementation("io.micrometer:micrometer-registry-prometheus:1.3.6")
    implementation("io.zipkin.brave:brave-instrumentation-sparkjava:5.10.1")
    implementation("io.zipkin.brave:brave-instrumentation-p6spy:5.10.1")
    implementation("io.zipkin.reporter2:zipkin-sender-urlconnection:2.12.1")

    implementation("com.h2database:h2:1.4.200")
    implementation("mysql:mysql-connector-java:8.0.19") {
        exclude(group: "com.google.protobuf", module: "protobuf-java")
    }
    implementation("com.zaxxer:HikariCP:3.4.2")

    testImplementation("org.assertj:assertj-core:3.15.0")
    testImplementation("io.rest-assured:rest-assured:4.3.0")
    testImplementation("org.reflections:reflections:0.9.12")

    testImplementation("org.testcontainers:mysql:1.13.0") {
        exclude(group: "org.slf4j", module: "slf4j-api")
    }

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.6.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.6.0")

    testImplementation("com.tngtech.archunit:archunit-junit5-api:0.13.1")
    testRuntimeOnly("com.tngtech.archunit:archunit-junit5-engine:0.13.1") {
        exclude(group: "org.junit.platform", module: "junit-platform-engine")
    }
}

application {
    mainClassName = "lt.rieske.accounts.App"
}
