plugins {
    id("account.library")
}

java {
    registerFeature("mysql") {
        usingSourceSet(sourceSets.main)
    }
    registerFeature("postgres") {
        usingSourceSet(sourceSets.main)
    }
}

configurations {
    integrationTestImplementation.extendsFrom(mysqlImplementation, postgresImplementation)
}

dependencies {
    implementation(libs.flyway)
    implementation(libs.slf4j)

    testFixturesApi(testFixtures(project(":account-service")))

    postgresImplementation(libs.postgres)
    postgresImplementation(libs.flywayPostgres)

    mysqlImplementation(libs.mysql) {
        exclude(group: "com.google.protobuf", module: "protobuf-java")
    }
    mysqlImplementation(libs.flywayMysql)

    testImplementation(libs.dbTestH2)

    integrationTestImplementation(testFixtures(project(":eventstore")))
    integrationTestImplementation(libs.dbTestPostgres)
    integrationTestImplementation(libs.dbTestMysql)
}
